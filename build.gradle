plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
}

group 'party.iroiro'
version '0.3.1'

sourceCompatibility = '1.9'
targetCompatibility = '1.9'

repositories {
    mavenCentral()
}

ext {
    r2dbcSpiVersion = '0.9.1.RELEASE'
    reactorVersion = '3.4.22'
    slf4jVersion = '2.0.0'
    lombokVersion = '1.18.24'
    springStarterVersion = '2.7.3'
}

java {
    withSourcesJar()
    withJavadocJar()

    registerFeature('spring') {
        usingSourceSet(sourceSets.main)
    }
}

dependencies {
    //noinspection GradlePackageUpdate -> TODO: Until Spring Data upgrades to R2DBC-SPI 1.0.0
    implementation "io.r2dbc:r2dbc-spi:$r2dbcSpiVersion"
    implementation "io.projectreactor:reactor-core:$reactorVersion"
    implementation 'party.iroiro:reactor-locks:1.1.0'
    implementation 'commons-beanutils:commons-beanutils:1.9.4'
    implementation "org.slf4j:slf4j-api:$slf4jVersion"
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    springImplementation "org.springframework.boot:spring-boot-starter-data-r2dbc:$springStarterVersion"

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testImplementation "io.projectreactor:reactor-test:$reactorVersion"
    //noinspection GradlePackageUpdate -> TODO: Until Spring Data upgrades to R2DBC-SPI 1.0.0
    testImplementation "io.r2dbc:r2dbc-spi-test:$r2dbcSpiVersion"
    testImplementation 'com.h2database:h2:2.1.214'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testImplementation 'org.assertj:assertj-core:3.23.1'
    testImplementation 'org.mockito:mockito-core:4.10.0'
    testImplementation "org.springframework.boot:spring-boot-starter-jdbc:$springStarterVersion"
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'R2DBC-Specification-Version': "$r2dbcSpiVersion"
        )
    }
}

apply from: 'jacoco.gradle'
apply from: 'javadoc.gradle'
apply from: 'publish.gradle'